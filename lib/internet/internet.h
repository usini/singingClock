
// WiFi
#include <WiFi.h>
extern "C"
{
#include "freertos/FreeRTOS.h"
#include "freertos/timers.h"
}

// MQTT
#include <AsyncMqttClient.h>

// NTP Management
#include <Preferences.h>
#include <ezTime.h>

// WiFi Settings
String wifiSSID = "";
String wifiPassword = "";

// NTP Settings
Timezone myTz;

bool connected = false;
bool wifiChange = false;
bool redrawInProgress = false;
bool mqttConnected = false;
bool wifiCredentialsNeededSave = false;

AsyncMqttClient mqttClient;
TimerHandle_t mqttReconnectTimer;
TimerHandle_t wifiReconnectTimer;

IPAddress mqtt_host = IPAddress(0, 0, 0, 0);
int mqtt_port = 1883;
const char *mqttUsername = "";
const char *mqttPassword = "";

bool buttonState[3] = {false, false, false};
bool buttonRedrawNeeded[3] = {false, false, false};

void connectToMqtt();

void WiFiTask(void *pvParameters)
{
    while (true)
    {
        if (connected)
        {
            events();
        }
        if (wifiChange)
        {
            connected = false;
            wifiChange = false;
            Serial.println("[ğŸ“¶ WIFI] ğŸ‘‹ğŸ·ï¸ SSID changed");
            wifiCredentialsNeededSave = true;
            WiFi.disconnect();
        }
        vTaskDelay(pdMS_TO_TICKS(100));
    }
}

void saveWifiCredentialsToFlash()
{
    File LittleFS_File = LittleFS.open("/wifi.txt", FILE_WRITE);
    LittleFS_File.println(wifiSSID);
    LittleFS_File.println(wifiPassword);
    LittleFS_File.close();
    Serial.println("[ğŸ’¾ FLASH] ğŸ“¶ WiFi Credentials saved to flash");
}

void WiFiEvent(WiFiEvent_t event)
{
    Serial.printf("[ğŸ“¶ WIFI] event: %d\n", event);
    switch (event)
    {
    case SYSTEM_EVENT_STA_GOT_IP:
        Serial.println("[ğŸ“¶ WIFI] ğŸŸ¢ OK");
        Serial.print("[ğŸ“¶ WIFI] IP: ");
        Serial.println(WiFi.localIP());
        connected = true;
        myTz.setLocation("Europe/Paris");
        Serial.println("[ğŸ“¶â²ï¸ NTP CLOCK] ğŸ‘‹ Init");
        if (wifiCredentialsNeededSave)
        {
            saveWifiCredentialsToFlash();
        }
        connectToMqtt();
        break;
    case SYSTEM_EVENT_STA_DISCONNECTED:
        connected = false;
        Serial.println("[ğŸ“¶ WIFI] ğŸ”´ FAILED");
        xTimerStop(mqttReconnectTimer, 0); // ensure we don't reconnect to MQTT while reconnecting to Wi-Fi
        xTimerStart(wifiReconnectTimer, 0);
        break;
    }
}

void connectToWifi()
{
    WiFi.begin(wifiSSID, wifiPassword);
    Serial.println("[ğŸ“¶ WIFI] ğŸ‘‹ Init");
}

void connectToMqtt()
{
    while (redrawInProgress)
    {
        Serial.println("Waiting for redraw");
        delay(100);
    }
    Serial.println("ğŸ“¨MQTT] ğŸ‘‹ Init");
    mqttClient.setServer(mqtt_host, mqtt_port);
    mqttClient.setCredentials(mqttUsername, mqttPassword);
    mqttClient.connect();
}

void onMqttConnect(bool sessionPresent)
{
    Serial.println("ğŸ“¨MQTT] ğŸŸ¢ OK");
    mqttClient.subscribe("lampe/salon/status", 2);
    mqttConnected = true;
}

void onMqttDisconnect(AsyncMqttClientDisconnectReason reason)
{
    Serial.println("ğŸ“¨MQTT] ğŸ”´ FAILED");
    mqttConnected = false;
    if (WiFi.isConnected())
    {
        xTimerStart(mqttReconnectTimer, 0);
    }
}

void onMqttSubscribe(uint16_t packetId, uint8_t qos)
{
    Serial.println("[ğŸ“¨MQTT] Subcribded");
}

void onMqttUnsubscribe(uint16_t packetId)
{
    Serial.println("Unsubscribe acknowledged.");
    Serial.print("  packetId: ");
    Serial.println(packetId);
}

void onMqttMessage(char *topic, char *payload, AsyncMqttClientMessageProperties properties, size_t len, size_t index, size_t total)
{
    Serial.print("[ğŸ“¨MQTT] Received - ");
    Serial.print(topic);
    Serial.print(" ");
    Serial.println(payload);
    if (String(topic) == "lampe/salon/status")
    {
        buttonRedrawNeeded[0] = true;
        if (String(payload) == "on")
        {
            buttonState[0] = true;
        }
        if (String(payload) == "off")
            buttonState[0] = false;
    }
}

void onMqttPublish(uint16_t packetId)
{
    Serial.println("[ğŸ“¨MQTT] Sended");
}

bool readWifiCredentialsFlash()
{
    if (LittleFS.exists("/wifi.txt"))
    {
        File LittleFS_File = LittleFS.open("/wifi.txt", FILE_READ);
        wifiSSID = LittleFS_File.readStringUntil('\n');
        wifiSSID.trim();
        wifiPassword = LittleFS_File.readStringUntil('\n');
        wifiPassword.trim();
        LittleFS_File.close();
        Serial.println("[ğŸ’¾ FLASH] ğŸŸ¢ /flash/wifi.txt founded");
        return true;
    }
    wifiCredentialsNeededSave = true;
    Serial.println("[ğŸ’¾ FLASH] ğŸ”´ /flash/wifi.txt not founded");
    return false;
}

bool readWifiCredentialsSD()
{
    if (SD.exists("/wifi.txt"))
    {
        File SD_File = SD.open("/wifi.txt", "r");
        String newWifiSSID = SD_File.readStringUntil('\n');
        newWifiSSID.trim();
        String newWifiPassword = SD_File.readStringUntil('\n');
        newWifiPassword.trim();
        if(newWifiSSID == wifiSSID && newWifiPassword == wifiPassword){
            Serial.println("[ğŸ’¾ SD] ğŸŸ¢ /sd/wifi.txt Same Credentials");
        } else {
            Serial.println("[ğŸ’¾ SD] ğŸŸ¢ /sd/wifi.txt New Credentials");
            wifiSSID = newWifiSSID;
            wifiPassword = newWifiPassword;
            wifiCredentialsNeededSave = true;
        }
        SD_File.close();
        return true;
    }
    Serial.println("[ğŸ’¾ SD] â” /sd/wifi.txt not founded");
    return false;
}

void wifiStart()
{
    readWifiCredentialsFlash();
    readWifiCredentialsSD();
    mqttReconnectTimer = xTimerCreate("mqttTimer", pdMS_TO_TICKS(2000), pdFALSE, (void *)0, reinterpret_cast<TimerCallbackFunction_t>(connectToMqtt));
    wifiReconnectTimer = xTimerCreate("wifiTimer", pdMS_TO_TICKS(2000), pdFALSE, (void *)0, reinterpret_cast<TimerCallbackFunction_t>(connectToWifi));

    WiFi.onEvent(WiFiEvent);

    mqttClient.onConnect(onMqttConnect);
    mqttClient.onDisconnect(onMqttDisconnect);
    mqttClient.onSubscribe(onMqttSubscribe);
    mqttClient.onUnsubscribe(onMqttUnsubscribe);
    mqttClient.onMessage(onMqttMessage);
    mqttClient.onPublish(onMqttPublish);

    xTimerStart(wifiReconnectTimer, 0);

    while (redrawInProgress)
    {
        Serial.println("Redraw in progress");
        delay(100);
    }
    xTaskCreate(
        WiFiTask,   // Fonction pour la tÃ¢che
        "WiFiTask", // Nom de la tÃ¢che
        6000,       // Taille de la pile pour la tÃ¢che
        NULL,       // ParamÃ¨tre pour la tÃ¢che
        0,          // PrioritÃ© de la tÃ¢che
        NULL        // Handle de la tÃ¢che
    );
}